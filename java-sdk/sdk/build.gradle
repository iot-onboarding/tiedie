// Copyright (c) 2023, Cisco and/or its affiliates.
// All rights reserved.
// See LICENSE file in this distribution.
// SPDX-License-Identifier: Apache-2.0

plugins {
    id 'java-library'
    id "io.freefair.lombok" version "8.0.0-rc4"
    id 'jacoco'
    id 'com.google.protobuf' version '0.9.2'
}

group 'com.cisco.tiedie'
version '1.0'

sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('com.squareup.okhttp3:okhttp-bom:4.10.0')
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5'
    implementation 'com.google.protobuf:protobuf-java:3.22.2'

    testImplementation 'com.squareup.okhttp3:mockwebserver'
    testImplementation platform('org.junit:junit-bom:5.8.2')
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation "org.testcontainers:testcontainers:1.17.6"
    testImplementation "org.testcontainers:junit-jupiter:1.17.6"
    testImplementation 'org.bouncycastle:bcpkix-jdk18on:1.76'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "com/cisco/tiedie/proto/**"
            ])
        }))
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.22.2'
    }
}
